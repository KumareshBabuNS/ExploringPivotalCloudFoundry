allprojects {
    apply plugin: 'eclipse-wtp'
    apply plugin: 'idea'

    eclipse {
        classpath {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '3.3'
    }

    ext.getSharedManifest = {
        return manifest {
            attributes('provider': 'gradle',
                    'Implementation-Title': project.name,
                    'Implementation-Version': project.version,
                    'Implementation-Description': project.description,
                    'Build-By': System.getProperty('user.name'),
                    'Build-JDK': System.getProperty('java.version'),
                    'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Source-Compatibility': project.sourceCompatibility,
                    'Target-Compatibility': project.targetCompatibility,
                    'VCS': gitBranch() + ", " + gitCommitId())
        }
    }
}

def gitBranch() {
    try {
        return file("$rootDir/.git/HEAD").text.trim() - "ref: refs/heads/"
    } catch (Exception e) {
        return "no-git-branch"
    }
}

def gitCommitId() {
    try {
        return file("$rootDir/.git/refs/heads/" + gitBranch()).text.trim()
    } catch (Exception e) {
        return "no-git-commit-id"
    }
}